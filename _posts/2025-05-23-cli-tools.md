---
layout: post
title: "tools.cli"
date: 2025-05-23
---
## Multiple arguments in the command line need a little help

Your project is humming along, adding feature after feature, sometimes multiple ways 
to do the same thing, hurray! But, your user of course will want to specify which 
way they want this session to go, and usually you need to know from the get-go. 

Having one option as a main argument is great, very easy, your code can be short and 
simple:
````
(defn -main [& args]
  (let [interface-type (keyword (first args))
        initial-state  {:status :config :interface (or interface-type :tui)}]
    (core/start-game initial-state)))
````
And then you think, ah, adding a second specified parameter will be easy peasy, just: 
````
(defn -main [& args]
  (let [interface-type (keyword (first args))
        save-type (keyword (second args))
        initial-state  {:status :config :interface (or interface-type :tui) :save (or save-type :sql)}]
    (core/start-game initial-state)))
````

But, wait, hold on! We have defaults set up, and, further, sometimes the user might not 
want to specify one of the parameters. That's fine, but.... this actually doesn't give 
a nice way for the user to specify only the second argument. If they want to specify the 
second argument, then they must also specify the first. That's more work than they probably 
want to do. (Me too, user, me too.)

Alas, another solution is needed. Thankfully, like most things, this problem has been 
faced by other programmers before me and they have a lovely little solution. Enter,
[tools.cli](https://github.com/clojure/tools.cli). 

You have to lay a little groundwork, like specifying what your options are and 
their possible tags as well as how to parse them if necessary. You have options to 
add helpful feedback to the user, but if you have a specified default that may not 
be necessary. 

It might look a little like: 

````
(def cli-options
  [["-i" "--interface TYPE" "Interface type"
    :default "tui"
    :validate [#(contains? #{"tui" "gui"} %)
               "Interface must be either 'tui' (default) or 'gui"]]
   ["-s" "--save TYPE" "Save type"
    :default "sql"
    :validate [#(contains? #{"sql" "edn"} %)
               "Save type must be 'sql' (default) or 'edn'"]]])

(defn -main [& args]
  (let [{:keys [options errors]} (cli/parse-opts args cli-options)
        initial-state  {:status :config :interface (keyword (:interface options))
                        :save (keyword (:save options))}]
    (if (seq errors)
      (display-errors errors)
      (core/start-game initial-state))))
````

This lets me specify the acceptable arguments (in the :validate parameter) and 
has a way to notify the user if they made an error (there's a display-errors function 
not quite interesting enough to show). 

However, This doesn't account for the fact that my acceptance criteria is just that 
a single flag must be accepted. So, if any labels are going to be avoided, 
then each flag must be its own label, and it gets a little less pretty (but 
more 'acceptable') 

````
(def cli-options
  [[nil "--gui" "Use GUI interface"]
   [nil "--tui" "Use TUI interface (default)"]
   [nil "--edn" "Save to EDN files"]
   [nil "--sql" "Save to SQL database (default)"]])

(defn -main [& args]
  (let [{:keys [options errors summary]} (cli/parse-opts args cli-options)
        interface (cond (:gui options) :gui
                        (:tui options) :tui
                        :else :tui)
        save (cond (:edn options) :edn
                   (:sql options) :sql
                   :else :sql)
        initial-state  {:status :config :interface interface :save save}]
    (if (seq errors)
      (display-errors errors summary)
      (core/start-game initial-state))))
````

The good news is, I could present the slightly cleaner option of flag-less 
parameters to my clients if they have any concerns about the use of flags. 

For now, I'll aim to deliver the requested product and sleep well knowing I 
have a solid alternative ready to go. 


**Happy Friday!**